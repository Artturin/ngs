ns {

	# Work in progress!
	# Flimsy code, needs refactor!

	section "Wrappers around C functions" {
		F _assert(code:Int, func:Str) {
			e = c_errno()
			if code == -1 {
				c_errno() throws CError(e, "${func}() failed")
				throw InvalidArgument("C code returned -1 but errno is not set")
			}
			code
		}

		global socket
		F socket(domain:Int, type:Int, protocol:Int=0) c_socket(domain, type, protocol)._assert('socket').assert(X>0)

		global bind
		F bind(socket:Int, address:c_sockaddr) c_bind(socket, address)._assert('bind').assert(0)

		TEST socket(C_PF_UNIX, C_SOCK_DGRAM) >= 0
		TEST $(ok:[0, 1] rm sock_test); bind(socket(C_PF_UNIX, C_SOCK_DGRAM), c_sockaddr_un("sock_test")) == 0

		global recvfrom
		F recvfrom(sock:Int, length:Int, flags:Int, address:c_sockaddr) {
			ret = c_recvfrom(sock, length, flags, address)
			ret[0]._assert('recvfrom')
			ret[1]
		}

		global listen
		F listen(sock:Int, backlog:Int) c_listen(sock, backlog)._assert('listen')

		global accept
		F accept(sock:Int, address:c_sockaddr) c_accept(sock, address)._assert('accept')

		global send
		F send(socket: Int, buffer:Str, flags:Int=0) c_send(socket, buffer, flags)._assert('send')
	}

	section "Higher level networking" {

		type Server
		type UnixStreamServer(Server)

		global init

		section "Connection" {
			type Connection
			F init(c:Connection, server:Server, sock:Int) {
				c.server = server
				c.sock = sock
				# For user data
				c.user = {}
			}

			global close
			F close(c:Connection) c.sock.close()

			global send
			F send(c:Connection, data:Str) c.sock.send(data)
		}

		section "ConnectionHandler" {
			type ConnectionHandler

			global on_connect
			F on_connect(ch:ConnectionHandler, c:Connection) {
				debug('server', 'ConnectionHandler on_connect()')
				ch.connection = c
			}

			global on_data
			F on_data(ch:ConnectionHandler, data:Str) {
				debug('server', 'ConnectionHandler on_data()')
			}

			global on_remote_close
			F on_remote_close(ch:ConnectionHandler) {
				debug('server', 'ConnectionHandler on_remote_close()')
				ch.connection.close()
			}
		}

		section "ServerHandler" {
			type ServerHandler
			F init(sh:ServerHandler) throw InvalidArgument()
			F init(sh:ServerHandler, make_connection_handler:Fun) init(args())

			global on_listen
			F on_listen(sh:ServerHandler, s:Server) {
				debug('server', 'ServerHandler on_listen()')
			}

			global on_end
			F on_end(sh:ServerHandler, s:Server) {
				debug('server', 'ServerHandler on_end()')
			}

		}

		section "ThreadedServerHandler" {
			type ThreadedServerHandler(ServerHandler)


			global on_connect
			F on_connect(sh:ThreadedServerHandler, c:Connection) {
				debug('server', 'ThreadedServerHandler on_connect()')

				# TODO: join the thread / use Executor
				Thread("connection-${c.id}", {
					try {
						debug('server', 'ThreadedServerHandler on_listen() - new thread')
						handler = sh::make_connection_handler()
						handler.on_connect(c)
						while true {
							data = recvfrom(c.sock, 1024, 0, c_sockaddr_un())
							if data == '' {
								handler.on_remote_close()
								debug('server', "After handler.on_remote_close()")
								break
							}
							debug('server', "ThreadedServerHandler data -- ${data.SafeStr().limit(1024)}")
							# c.send("[${c.sock}] Echoing back: ${data}")
							handler.on_data(data)
							debug('server', "ThreadedServerHandler on_connect -- after handler.on_data()")
						}
					} catch(e) {
						log("ThreadedServerHandler - exception in thread: ${e}")
						guard false
					}
				})
			}

		}

		section "UnixStreamServer" {
			F listen(tus:UnixStreamServer, path:Str, sh:ServerHandler) {
				id = 1
				sock = socket(C_PF_UNIX, C_SOCK_STREAM)
				tus.sock = sock
				debug('server', "Sock: ${sock}")
				$(ok:[0, 1] rm $path)
				bind(sock, c_sockaddr_un(path))
				remote_addr = c_sockaddr_un()
				listen(sock, 5)
				sh.on_listen(tus)
				while true {
					accepted = accept(sock, remote_addr)
					debug('server', "Connection socket: ${accepted}")
					c = Connection(tus, accepted).set('id', id)
					id += 1
					sh.on_connect(c)
					# TODO: how to break the loop?
				}
				sh.on_end(tus)
			}
		}

		section "LinesConnectionHandler" {
			# TODO: refactor. creating "next" ConnectionHandler should not be
			#       the responsibility of the previous ConnectionHandler.

			type LinesConnectionHandler(ConnectionHandler)

			doc make_connection_handler - next ConnectionHandler in chain
			F init(lch:LinesConnectionHandler) throw InvalidArgument()

			F init(lch:LinesConnectionHandler, make_connection_handler:Fun) {
				debug('server', "LinesConnectionHandler init()")
				init(args())
			}

			F on_connect(lch:LinesConnectionHandler, c:Connection) {
				lch.connection = c
				lch.buf = ''
				lch.next = lch::make_connection_handler()
				lch::next.on_connect(c)
			}

			F on_data(lch:LinesConnectionHandler, data:Str) {
				lch.buf += data
				while true {
					# TODO: start the search from previous position
					# TODO: handle "\r\n"
					p = pos(lch.buf, "\n")
					p == null breaks
					line = lch.buf[0..p]
					lch::next.on_data(line)
					lch.buf = lch.buf[(p+1)..null]
				}
			}

			F on_remote_close(lch:LinesConnectionHandler) {
				if lch.buf {
					lch::next.on_data(lch.buf)
					lch.buf = ''
				}
				lch::next.on_remote_close()
			}
		}

		section "JSON-RPC ConnectionHandler" {

			# TODO: optional "params" and "id"
			_valid_json_rpc_request = {
				'jsonrpc': '2.0'
				'method': Str
			}

			# https://www.jsonrpc.org/specification
			type JsonRpcConnectionHandler(ConnectionHandler)
			F init(jrch:JsonRpcConnectionHandler) throw InvalidArgument()

			F init(jrch:JsonRpcConnectionHandler, methods:Namespace) {
				debug('server', "JsonRpcConnectionHandler init()")
				init(args())
			}

			F on_data(jrch:JsonRpcConnectionHandler, data:Str) block b {
				debug('server', "JsonRpcConnectionHandler on_data()")

				F send_error(code, message, data=null) {
					error("JsonRpcConnectionHandler on_data send_error -- ${code} ${message} ${data}")
					ret = {
						'jsonrpc': '2.0'
						'error': {
							'code': code
							'message': message
							'data': data
						}.filterv()
					}
					jrch.connection.send(ret.encode_json() + "\n")
					b.return()

				}

				req = try {
					data.decode_json()
				} catch(e:JsonDecodeFail) {
					send_error(-32700, 'Parse error', e.Hash().filterk(AnyOf('error', 'value', 'position')))
				}

				if not(req =~ _valid_json_rpc_request) send_error(-32600, 'Invalid Request')

				if not(req.method in jrch.methods) send_error(-32601, 'Method not found', "Method ${req.method} not found. Available methods: ${jrch.methods.keys()}")

				# TODO: Implement "Invalid params" mechanism
				result = try {
					debug('server', "JsonRpcConnectionHandler on_data -- invoking ${req.method}")
					jrch.methods[req.method](req)
				} catch(e) {
					send_error(-32603, 'Internal error', e.Str())
				}

				if 'id' in req {
					# response expected
					ret = {
						'jsonrpc': '2.0'
						'id': req.id
						'result': result
					}.filterv()
					debug('server', 'JsonRpcConnectionHandler on_data -- sending reply')
					jrch.connection.send(ret.encode_json() + "\n")
				}

			}

		}

		F test_un_server() {
			section "EchoConnectionHandler" {
				type EchoConnectionHandler(ConnectionHandler)
				global on_data
				F on_data(ech:EchoConnectionHandler, data:Str) {
					debug('server', 'EchoConnectionHandler on_data()')
					ech.connection.send("[${ech.connection.id}] Echoing back (escaped) line: ${data.SafeStr()}\n")
				}

				global on_remote_close
				F on_remote_close(ech:EchoConnectionHandler) {
					debug('server', 'EchoConnectionHandler on_remote_close()')
					super(ech)
				}
			}
			# sh = ThreadedServerHandler(EchoConnectionHandler)
			sh = ThreadedServerHandler(F on_connect() {
				LinesConnectionHandler(EchoConnectionHandler)
			})
			net::UnixStreamServer().listen("sock_test", sh)
		}

		# DEBUG=server ngs -pi 'net::test_un_server()'
		# echo wwwxyz | nc -U build/sock_test
	}
}

